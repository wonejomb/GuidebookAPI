import me.modmuss50.mpp.ReleaseType

plugins {
    id 'java-library'
    id 'idea'
    id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    id 'net.neoforged.moddev' version '2.0.6-beta'
    id 'me.modmuss50.mod-publish-plugin' version '0.4.2'
}

group="de.wonejo.wuidebook"
version="${mod_version}-neoforge"
base.archivesName="[${mc_version}]${mod_name}"

java {
    toolchain {
        languageVersion=JavaLanguageVersion.of(21)
        vendor=JvmVendorSpec.MICROSOFT
    }

    withSourcesJar()
    withJavadocJar()
    sourceCompatibility=JavaVersion.VERSION_21
    targetCompatibility=JavaVersion.VERSION_21
}

sourceSets {
    main {
        resources.srcDirs += project(":Common").sourceSets.main.resources.srcDirs
    }
}

repositories {
    mavenCentral ()
}

dependencies {
    compileOnly project(":Common")
}

neoForge {
    version = "${neo_version}"

    parchment {
        minecraftVersion="${parchment_mc}"
        mappingsVersion="${parchment_version}"
    }

    runs {
        configureEach {
            systemProperty 'neoforge.logging.markers', 'REGISTRIES'
            systemProperty 'neoforge.logging.console.level', 'debug'
            logLevel = org.slf4j.event.Level.DEBUG
        }
        client {
            client()
            gameDirectory = project.file('run/client')
            environment = ["DevWorkspace": "true"]
            ideName="NeoForge Client"
            systemProperty 'neoforge.enabledGameTestNamespaces', "${mod_id}"
        }
        server {
            server()
            gameDirectory = project.file('run/server')
            environment = ['DevWorkspace': 'true']
            ideName="NeoForge Server"
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', "${mod_id}"
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}