import me.modmuss50.mpp.ReleaseType

plugins {
    id 'java-library'
    id 'idea'
    id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    id 'net.neoforged.moddev' version '0.1.108'
    id 'me.modmuss50.mod-publish-plugin' version '0.4.2'
}

group="de.wonejo.gapi"
version="${mod_version}-neoforge"
base.archivesName="[${mc_version}]${mod_name}"

java {
    toolchain {
        languageVersion=JavaLanguageVersion.of(21)
        vendor=JvmVendorSpec.MICROSOFT
    }

    withSourcesJar()
    withJavadocJar()
    sourceCompatibility=JavaVersion.VERSION_21
    targetCompatibility=JavaVersion.VERSION_21
}

sourceSets {
    main {
        resources.srcDirs += project(":Common").sourceSets.main.resources.srcDirs
    }
}

repositories {
    mavenCentral ()
}

dependencies {
    compileOnly project(":Common")
}

neoForge {
    version = "${neo_version}"

    parchment {
        minecraftVersion="${parchment_mc_version}"
        mappingsVersion="${parchment_version}"
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
            logLevel = org.slf4j.event.Level.DEBUG
        }
        client {
            client()
            gameDirectory = project.file('run/client')
            environment = ["DevWorkspace": "true"]
            ideName="NeoForge Client"
            systemProperty 'neoforge.enabledGameTestNamespaces', "${mod_id}"
        }
        server {
            server()
            gameDirectory = project.file('run/server')
            environment = ['DevWorkspace': 'true']
            ideName="NeoForge Server"
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', "${mod_id}"
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

publishMods {
    type = ReleaseType.of(release_type)
    file = jar.archiveFile.get()
    modLoaders.add('neoforge')
    version="${mc_version}-${mod_version}"
    displayName="${mod_name} ${mc_version}-${mod_version}"

    modrinth {
        accessToken = findProperty("MODRINTH_API") ?: "DUMMY"
        projectId = modrinth_id
        minecraftVersions.add mc_version
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}