import me.modmuss50.mpp.ReleaseType

plugins {
    id 'java-library'
    id 'idea'
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'net.neoforged.moddev' version '2.0.6-beta' apply false
    id 'me.modmuss50.mod-publish-plugin' version '0.4.2'
}

group='de.wonejo.wuidebook'
version="${mod_version}-fabric"
base.archivesName="[${mc_version}]${mod_name}"

java {
    toolchain {
        languageVersion=JavaLanguageVersion.of(21)
        vendor=JvmVendorSpec.MICROSOFT
    }

    withSourcesJar()
    withJavadocJar()
    sourceCompatibility=JavaVersion.VERSION_21
    targetCompatibility=JavaVersion.VERSION_21
}

sourceSets {
    main {
        resources.srcDirs += ['src/generate/resources', project(":Common").sourceSets.main.resources.srcDirs ]
    }
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    mavenCentral ()
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered {
        officialMojangMappings ()
        parchment ("org.parchmentmc.data:parchment-${parchment_mc}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api}"

    compileOnly project(":Common")
}

loom {
    runs {
        named('client') {
            client()
            configName="Fabric Client"
            ideConfigGenerated=true
            runDir('run/client')
            environmentVariable('DevWorkspace', 'client')
            programArg '--username=Dev'
        }
        named('server') {
            server()
            configName="Fabric Server"
            ideConfigGenerated=true
            runDir('run/server')
            environmentVariable('DevWorkspace', 'client')
            programArg '--nogui'
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}